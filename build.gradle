plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id('com.gradleup.shadow') version '8.3.0'
}

group = 'com.argemtum'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = 'glaremasters repo'
        url = 'https://repo.glaremasters.me/repository/towny/'
    }
    maven {
        url "https://maven.enginehub.org/repo/"
    }
}
configurations {
    townyPlugin
}

// Resolve WorldGuard dependencies conflict
configurations.configureEach {
    resolutionStrategy {
        force 'com.google.guava:guava:32.1.3-jre'
        force 'com.google.code.gson:gson:2.10.1'
        force 'it.unimi.dsi:fastutil:8.5.12'
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.10-R0.1-SNAPSHOT")
    compileOnly("com.palmergames.bukkit.towny:towny:0.102.0.0")
    compileOnly('com.sk89q.worldguard:worldguard-bukkit:7.0.11')
    implementation 'com.google.inject:guice:7.0.0'

    // for coping to run/plugins
    townyPlugin("com.palmergames.bukkit.towny:towny:0.102.0.0")
}

shadowJar {
    archiveClassifier = ''
    minimize()
    relocate 'com.google.inject', 'com.argemtum.shadow.guice'
}

build {
    dependsOn("shadowJar")
}

tasks.register("copyTowny", Copy) {
    from configurations.townyPlugin
    into layout.projectDirectory.dir("run/plugins")
}


tasks.named("runServer") {
    dependsOn("copyTowny")
    dependsOn("shadowJar")
    minecraftVersion("1.21")
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
